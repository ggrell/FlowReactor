apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'io.gitlab.arturbosch.detekt'

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:${versions.detekt}"

    implementation deps.kotlin.stdlib.common
    implementation deps.kotlin.coroutines.core

    testImplementation deps.junit
    testImplementation deps.kotlin.coroutines.test
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

jacoco {
    toolVersion = versions.jacoco
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

detekt {
    config = files("detekt.yml")
    baseline = file("detekt-baseline.xml")
    buildUponDefaultConfig = true
    reports {
        xml {
            enabled = true
            destination = file("build/reports/detekt/detekt.xml")
        }
        html {
            enabled = true
            destination = file("build/reports/detekt/detekt.html")
        }
        txt {
            enabled = false
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        flowReactor(MavenPublication) {
            groupId = project.group
            artifactId = archivesBaseName

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "FlowReactor for Kotlin Coroutines"
                description = "A Kotlin framework for a reactive and unidirectional application architecture"
                url = siteUrl

                licenses {
                    license {
                        name = 'BSD 3-Clause License'
                        url = 'https://github.com/ggrell/RxReactor/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'ggrell'
                        name = 'Gyuri Grell'
                        email = 'ggrell@pobox.com'
                    }
                }

                scm {
                    connection = gitUrl
                    developerConnection = gitUrl
                    url = siteUrl
                }
            }
        }
    }
}